plugins {
    id 'fabric-loom' version '0.2.0-SNAPSHOT'
    id 'scala'
    id 'org.ajoberstar.reckon' version '0.8.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = 'stockpile'

minecraft {
}

reckon {
    scopeFromProp()
    stageFromProp('beta', 'rc', 'final')
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url 'https://jitpack.io' }
}

// For Mixin's annotation processor to work, we need to compile Java independently from Scala.

tasks.compileJava.dependsOn compileScala
tasks.compileScala.dependsOn.remove("compileJava")

sourceSets {
    main {
        scala {
            outputDir = file("$buildDir/classes/scala/main")
        }

        java {
            outputDir = file("$buildDir/classes/java/main")
        }
    }
}

dependencies {
    minecraft 'com.mojang:minecraft:19w02a'

    mappings 'net.fabricmc:yarn:19w02a.12'

    modCompile 'net.fabricmc:fabric-loader:0.3.2.92'
    modCompile 'net.fabricmc:fabric:0.1.4.71'
    modCompile 'net.fabricmc:fabric-language-scala:0.1.0'

    modCompile 'com.github.notjoe7F:cereal:2.0.1'

    compile 'org.scala-lang:scala-library:2.12.8'

    // Scala seems unhappy without this! It provides @Nullable.
    compile 'com.google.code.findbugs:jsr305:3.0.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.scalatest:scalatest_2.12:3.0.5'
    testCompile 'org.powermock:powermock-module-junit4:1.7.1'
    testCompile 'org.powermock:powermock-api-mockito2:1.7.1'

    compile files("$sourceSets.main.scala.outputDir")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

processResources {
    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}

reckonTagCreate.dependsOn check