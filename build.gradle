plugins {
    id 'fabric-loom' version '0.2.0-SNAPSHOT'
    id 'org.ajoberstar.grgit' version '3.0.0'
    id 'scala'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = 'stockpile'

def mcVersion = '19w06a'
def isRelease = project.hasProperty("release")

version = '0.4.5'

if (!isRelease) {
    def ciBuildNumber = System.getenv("CIRCLE_BUILD_NUM") ?: 0
    def currentBranch = grgit.branch.current().name ?: "unknown"
    version += "-$currentBranch.$ciBuildNumber"
}

version += "+$mcVersion"

minecraft {
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url 'https://jitpack.io' }
}

// For Mixin's annotation processor to work, we need to compile Java independently from Scala.

tasks.compileJava.dependsOn compileScala
tasks.compileScala.dependsOn.remove("compileJava")

sourceSets {
    main {
        scala {
            outputDir = file("$buildDir/classes/scala/main")
        }

        java {
            outputDir = file("$buildDir/classes/java/main")
        }
    }
}

dependencies {
    minecraft 'com.mojang:minecraft:19w06a'

    mappings 'net.fabricmc:yarn:19w06a.3'

    modCompile 'net.fabricmc:fabric-loader:0.3.5.106'
    modCompile 'net.fabricmc:fabric:0.2.0.92'
    modCompile 'net.fabricmc:fabric-language-scala:0.1.0'

    modCompile 'com.github.notjoe7F:cereal:3.0.0-beta.1'

    compile 'org.scala-lang:scala-library:2.12.8'

    // Scala seems unhappy without this! It provides @Nullable.
    compile 'com.google.code.findbugs:jsr305:3.0.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.scalatest:scalatest_2.12:3.0.5'
    testCompile 'org.powermock:powermock-module-junit4:1.7.1'
    testCompile 'org.powermock:powermock-api-mockito2:1.7.1'

    compile files("$sourceSets.main.scala.outputDir")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

processResources {
    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}